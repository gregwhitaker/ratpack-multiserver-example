import com.bmuschko.gradle.docker.tasks.image.*

plugins {
    id 'com.gradle.build-scan' version '1.13.1'
    id 'io.ratpack.ratpack-java' version '1.5.4'
    id 'com.bmuschko.docker-remote-api' version '3.2.1'
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishAlways()
}

group = 'com.github.gregwhitaker'
version = '0.1.0'
sourceCompatibility = 1.8
mainClassName = 'ratpack.multiserver.example.Main'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile ratpack.dependency('guice')

    runtime 'org.slf4j:slf4j-simple:1.7.22'
}

distZip {
    // You only need to set this if you have specified an archive version. This removes the version number
    // from the final distribution package which makes it easier to script in your Dockerfile.
    archiveName = 'ratpack-multiserver-example.zip'
}

task buildImage(type: DockerBuildImage) {
    inputDir = new File("${projectDir}")
    dockerFile = new File("${projectDir}/src/docker/Dockerfile")
}
